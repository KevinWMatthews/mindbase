statement = _{ SOI ~ (artifactstatement | symbolstatement)? ~ ( EOI | "#" ) }

symbolvar = ${ "$" ~ literal }
artifactvar = ${"@" ~ literal}

// You can symbolize without storing the symbovar
// $foo : Symbolize(..)
// Symbolize(..)
symbolstatement = { (symbolvar ~ "=")? ~ symbol }

// But there's not much point to doing so for an artifact, so the artifactvar is not optional
// @foo : Url("http://...")
artifactstatement = { artifactvar ~ "=" ~ artifact }

// Symbols
symbol    = { symbolvar | ground | symbolize | alledge }
ground = { "{" ~ symbolizable+ ~ (WHITESPACE+ ~ symbolizable )* ~ "}" }
symbolize = { "Symbolize(" ~ symbolizable ~ ")" }
alledge =  { "Alledge(" ~ symbol_pair ~ ")" }

// Artifacts
artifact  = { artifactvar | agent | url | text | datagraph | datanode | datarelation }
agent     = { "Agent(" ~ ("default"|ASCII_ALPHANUMERIC{32}) ~ ")" }
url       = { "Url(" ~ quoted_string ~ ")" }
text      = { "Text(" ~ quoted_string ~ ")" | quoted_string }
datagraph    = { "DataGraph(" ~ quoted_string ~ ")" }
datanode     = { "DataNode(" ~ quoted_string ~ ")" }
datarelation = { "DataRelation(" ~ quoted_string ~ ")" }


literal = { (ASCII_ALPHANUMERIC | "_")+ }
quoted_string = ${ "\"" ~ string ~ "\"" }
string = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

symbolizable = _{ artifact | symbol | "(" ~ symbol_pair ~ ")" }
symbol_pair = { symbolizable ~ ":" ~ symbolizable }

WHITESPACE = _{ " " | "\t" }