statement = _{ SOI ~ (artifactstatement | symbolstatement)? ~ ( EOI | "#" ) }

symbolvar = ${ "$" ~ literal }
artifactvar = ${ "@" ~ literal }

// You can symbolize without storing the symbovar
// $foo : Symbolize(..)
// Symbolize(..)
symbolstatement = { (symbolvar ~ "=")? ~ ( allege | ground | symbolize ) }

// But there's not much point to doing so for an artifact, so the artifactvar is not optional
// @foo : Url("http://...")
artifactstatement = { artifactvar ~ "=" ~ artifact }

// Symbols
ground = { ( "Ground(" ~ (ground_symbol_pair | ground_symbolizable) ~ ")") | ("{" ~ (ground_symbol_pair|ground_symbolizable) ~ "}") }
symbolize = { "Symbolize(" ~ symbolizable ~ ")" }
allege =  { "Allege(" ~ symbol_pair ~ ")" }

// Artifacts
artifact  = { artifactvar | agent | url | text | datanode | datarelation }
agent     = { "Agent(" ~ agent_ident ~ ")" }
agent_ident = { "default" | ASCII_ALPHANUMERIC{32} }
url       = { "Url(" ~ quoted_string ~ ")" }
text      = { "Text(" ~ quoted_string ~ ")" | quoted_string }
datanode     = { "DataNode(" ~ symbolizable ~ (";" ~ (quoted_string | base64))? ~ ")" }
datarelation = { "DataRelation(" ~ symbolizable  ~ ";" ~ symbolizable  ~ ">" ~ symbolizable ~ ")" }

base64 = @{ base64_char+ ~ "="{,2} }
base64_char = { '0'..'9' | 'a'..'z' | 'A'..'Z' | "+" | "/"  }
literal = { (ASCII_ALPHANUMERIC | "_")+ }
quoted_string = ${ "\"" ~ string ~ "\"" }
string = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

symbolizable = { artifact | symbolvar | ground | symbolize | allege | "(" ~ symbol_pair ~ ")" }
symbol_pair = { symbolizable ~ ":" ~ symbolizable }

// You can use a symbolvar, but you can't Symbolize or Allege in a ground symbolizable
ground_symbolizable = { artifact | symbolvar | ground | "(" ~ ground_symbol_pair ~ ")" }
ground_symbol_pair = { ground_symbolizable ~ ":" ~ ground_symbolizable }

WHITESPACE = _{ " " | "\t" }